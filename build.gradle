plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id 'org.ajoberstar.git-publish' version '3.0.0'
}

group = 'com.trophonix'
version = '3.84'
description = 'TradePlus'

repositories {
    mavenLocal()
    maven { url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/') }
    maven { url = uri('https://oss.sonatype.org/content/repositories/snapshots') }
    maven { url = uri('https://nexus.hc.to/content/repositories/pub_releases') }
    maven { url = uri('https://repo.dakanilabs.com/repository/maven-public/') }
    maven { url = uri('https://jitpack.io') }
    maven { url = uri('https://repo.aikar.co/content/groups/aikar/') }
    maven { url = uri('https://repo.codemc.org/repository/maven-public/') }
    maven { url = uri('https://repo.maven.apache.org/maven2/') }

    maven {
        url("https://repo.coralmc.it/releases")
        credentials {
            username "$mavenUser"
            password "$mavenPassword"
        }

        authentication {
            basic(BasicAuthentication)
        }
    }

}

dependencies {
    compileOnly 'it.coralrp.laroc:api:1.0.9-SNAPSHOT'
    implementation 'com.google.guava:guava:21.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'net.wesjd:anvilgui:1.9.0-SNAPSHOT'
    implementation 'co.aikar:taskchain-bukkit:3.7.2'
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

tasks.register('sourceJar', Jar) {
    from sourceSets.main.allJava
}

shadowJar {
    archiveBaseName.set('TradePlus')
    archiveClassifier.set('')
    archiveVersion.set('')
}

processResources {
    expand(project: project)
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}